//see this: https://dev.to/techschoolguru/config-gradle-to-generate-java-code-from-protobuf-1cla
//note that the protobuf plugin will default to picking up proto files from src/main/proto
//but you can specify different paths in the protobuf{} section of the gradle file



plugins {
    id 'java-library'
    id "com.google.protobuf" version "0.9.5"
    id 'maven-publish'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.5'
}

group 'io.mgrpc'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

def grpcVersion = '1.72.0'

dependencies {


    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.26'


    api group: 'com.google.protobuf', name: 'protobuf-java', version: '4.30.2'
    //For StatusException, Status, Channel ..
    api group: 'io.grpc', name: 'grpc-api', version: grpcVersion
    //For StreamObserver
    api group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
    //For errors using com.google.rpc.Status and other dependencies in protoc generated code
    api group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion

    //mqtt support
    api group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'

    //JMS support
    api group:"org.apache.activemq", name: "artemis-jms-client", version: "2.29.0"

    //For https proxy server
    implementation group: "io.grpc", name: "grpc-netty", version: grpcVersion


    //Need this because grpc will generate code that has annotations from javax.annotation (@generated)
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        api 'javax.annotation:javax.annotation-api:1.3.2'
    }
    //For InProcess server
    testImplementation "io.grpc:grpc-core:${grpcVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.platform:junit-platform-suite:1.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    //For tests that use inprocess
    testImplementation group: "io.grpc", name: "grpc-inprocess", version: grpcVersion


    //For TestAuth
    testImplementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    testImplementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

    //For testing with embedded artemis broker with mqtt
    testImplementation group:"org.apache.activemq", name: "artemis-server", version: "2.29.0"
    testImplementation group:"org.apache.activemq", name: "artemis-mqtt-protocol", version: "2.29.0"

}

//Tell gradle and intellij about where the generated proto java files are
sourceSets {
    main {
        java {
            //Generated message classes for protocol buffers will be here
            srcDirs 'build/generated/source/proto/main/java'
            //Generated grpc classes will be here (stubs, services etc)
            srcDirs 'build/generated/source/proto/main/grpc'
            //Generated message classes for protocol buffers for tests will be here
            srcDirs 'build/generated/source/proto/test/java'
            //Generated grpc classes for tests will be here (stubs, services etc)
            srcDirs 'build/generated/source/proto/test/grpc'
        }
    }
}


java {

    //Make sure that the source code is compliant with 1.8 so that people have the possibility
    //to build this against a with a java 1.8 toolchain so that they can run with older VMs.
    //(grpc-java is fully compatible with java 1.8)
    sourceCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

jar {
    exclude '*.proto'
    includeEmptyDirs false
}

//Make it possible to publish sources to maven repo.
task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}
//Do this or else gradle will fail to  build with the error:
//Task ':sourceJar' uses this output of task ':generateProto' without declaring an explicit or implicit dependency.
//This can lead to incorrect results being produced, depending on what order the tasks are executed.
afterEvaluate {
    sourceJar.dependsOn tasks.generateProto
}


//For shadow jar
application {
    mainClassName = 'org.example.Main'
}
tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    archiveBaseName.set('mgrpc-proxy')
    archiveClassifier.set('mgrpc-proxy')
    archiveVersion.set('')
}


publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

//Configuration for the com.google.protobuf gradle plugin
protobuf {
    //This will pull down the protoc compiler from maven so there is no need to install it separately
    protoc {
        artifact = 'com.google.protobuf:protoc:4.30.2'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

//only necessary if protoc is not on path and if the maven protoc compiler is not being used.
//protobuf {
//    protoc {
//        path = 'c:/bin/protoc/bin/protoc'
//    }
//}

