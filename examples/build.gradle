plugins {
    id 'java'
    id "application"
    id "com.google.protobuf" version "0.9.5"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

def grpcVersion = '1.72.0'

dependencies {

    implementation group: 'io.mgrpc', name: 'mgrpc', version: '1.0-SNAPSHOT'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '4.30.2'
    //protobuf-java-util is only needed for JsonFormat for the RouteGuide example
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '4.30.2'
    implementation group: 'io.grpc', name: 'grpc-api', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion

    implementation group: "io.grpc", name: "grpc-netty", version: grpcVersion

    implementation group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'


    //For testing with embedded artemis broker with mqtt
    implementation group:"org.apache.activemq", name: "artemis-server", version: "2.29.0"
    implementation group:"org.apache.activemq", name: "artemis-mqtt-protocol", version: "2.29.0"


    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
    }


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

//Tell gradle and intellij about where the generated proto java files are
sourceSets {
    main {
        java {
            //Generated message classes for protocol buffers will be here
            srcDirs 'build/generated/source/proto/main/java'
            //Generated grpc classes will be here (stubs, services etc)
            srcDirs 'build/generated/source/proto/main/grpc'
            //Generated message classes for protocol buffers for tests will be here
            srcDirs 'build/generated/source/proto/test/java'
            //Generated grpc classes for tests will be here (stubs, services etc)
            srcDirs 'build/generated/source/proto/test/grpc'
        }
    }
}

//Configuration for the com.google.protobuf gradle plugin
protobuf {
    //This will pull down the protoc compiler from maven so there is no need to install it separately
    protoc {
        artifact = 'com.google.protobuf:protoc:4.30.2'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}


if (project.hasProperty("args")) {
    ext.javaMainClass = "org.example.greeter." + project.getProperty("args")
} else {
    ext.javaMainClass = "org.example.greeter.GreeterService"
}

application {
    mainClassName = javaMainClass
}

test {
    useJUnitPlatform()
}